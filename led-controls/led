#!/usr/bin/perl
use strict;
use warnings;

my $LED_DIR = "/sys/devices/platform/thinkpad_acpi/leds";

if(`whoami` ne "root\n"){
  my @cmd = ('sudo', $0, @ARGV);
  exec @cmd;
}

my @validLeds = `ls $LED_DIR`;
s/^tpacpi(::|:)//g foreach @validLeds;
chomp foreach @validLeds;
@validLeds = sort @validLeds;
my $usage = ''
  . "Usage: $0 <led> [<on|off|toggle> | brightness]\n"
  . " LED name must be [a-zA-Z0-9:_-]+\n"
  . " {only letters, numbers, colons, underscores or hyphens}\n"
  . " tpacpi: is automatically prepended if it is not present\n"
  . " available LEDs: @validLeds\n"
;

my $name = shift;
die $usage if not defined $name;

$name = '' if not defined $name;
$name =~ s/^tpacpi://;
$name = ":$name" if $name !~ /:/;
$name = "tpacpi:$name";

if($name !~ /^([a-zA-Z0-9:_-]+)$/){
  die $usage;
}
$name = $1; #untaint

my $brightness = shift;
$brightness = 'toggle' if not defined $brightness;
if(@ARGV > 0 or $brightness !~ /^(on|off|toggle|\d+)$/i){
  die $usage;
}
$brightness = $1; #untaint

my $dev = "$LED_DIR/$name/brightness";

if(not -e $dev){
  die "No led device found at $dev\n";
}

sub readDev(){
  open FH, "< $dev";
  my @out = <FH>;
  close FH;
  return join '', @out;
}
sub writeDev($){
  my $val = shift;
  open FH, "> $dev";
  print FH "$val\n";
  close FH;
}

my $cur = readDev();
chomp $cur;


$brightness = 1 if lc $brightness eq 'on';
$brightness = 0 if lc $brightness eq 'off';
$brightness = 1-$cur if lc $brightness eq 'toggle';


writeDev $brightness;
print "LED $name $cur => " . readDev();

