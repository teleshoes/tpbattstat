#!/usr/bin/perl
use strict;
use warnings;

my $LED_DIR = "/sys/devices/platform/thinkpad_acpi/leds";

if(`whoami` ne "root\n"){
  my @cmd = ('sudo', $0, @ARGV);
  exec @cmd;
}

my @validLeds = `ls $LED_DIR`;
s/^tpacpi(::|:)//g foreach @validLeds;
chomp foreach @validLeds;
@validLeds = sort @validLeds;
my $usage = ''
  . "Usage: $0 <led-name> [<on|off|toggle> | brightness-integer]\n"
  . "  led-name must be [a-zA-Z0-9:_-]+\n"
  . "    {only letters, numbers, colons, underscores or hyphens}\n"
  . "  LED devices are in $LED_DIR/<led-name>/brightness\n"
  . "  the prefix 'tpacpi:' is removed from name if present, and either-\n"
  . "    1) tpacpi:: is prepended if name does NOT have a colon in it\n"
  . "    2) tpacpi: is prepended if name DOES have a colon in it\n"
  . "  e.g.:\n"
  . "    'tpacpi:green:batt'  =>  'tpacpi:green:batt'\n"
  . "    'green:batt'         =>  'tpacpi:green:batt'\n"
  . "    'mute_mic'           =>  'tpacpi::mute_mic'\n"
  . "    'tpacpi:mute_mic'    =>  'tpacpi::mute_mic'\n"
  . "    'tpacpi::mute_mic'   =>  'tpacpi::mute_mic'\n"
  . "  available LEDs: @validLeds\n"
;

my $name = shift;
die $usage if not defined $name;

$name = '' if not defined $name;
$name =~ s/^tpacpi://;
$name = ":$name" if $name !~ /:/;
$name = "tpacpi:$name";

if($name !~ /^([a-zA-Z0-9:_-]+)$/){
  die $usage;
}
$name = $1; #untaint

my $brightness = shift() || 'toggle';
if(@ARGV > 0 or $brightness !~ /^(on|off|toggle|\d+)$/i){
  die $usage;
}

my $dev = "$LED_DIR/$name/brightness";

if(not -e $dev){
  die "No led device found at $dev\n";
}

sub readDev(){
  open FH, "< $dev";
  my @out = <FH>;
  close FH;
  return join '', @out;
}
sub writeDev($){
  my $val = shift;
  open FH, "> $dev";
  print FH "$val\n";
  close FH;
}

my $cur = readDev();
chomp $cur;


$brightness = 1 if lc $brightness eq 'on';
$brightness = 0 if lc $brightness eq 'off';
$brightness = 1-$cur if lc $brightness eq 'toggle';


writeDev $brightness;
print "LED $name $cur => " . readDev();

